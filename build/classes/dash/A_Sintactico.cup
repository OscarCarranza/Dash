/*--------- 1ra Area: Codigo de Usuario---------*/

//---------> Paquetes, importaciones
package dash;
import java_cup.runtime.Symbol;
import java.util.LinkedList;
import Nodos.Nodo;

//---------> Codigo para el parser, variables, metodos
parser code
{:
    public String resultado="";
    public static LinkedList<MyError> listaErrorSintacticos = new LinkedList<MyError>();

    //Metodo al que se llama automaticamente ante algun error sintactico
    public void syntax_error(Symbol s)
    {        
        String lexema = s.value.toString();
        int fila = s.right;
        int columna = s.left;
        
        System.out.println("!!!!!!! Error Sintactico Recuperado !!!!!!!");
        System.out.println("\t\tLexema: "+lexema);
        System.out.println("\t\tFila: "+fila);
        System.out.println("\t\tColumna: "+columna);
        
        MyError datos = new MyError(lexema, fila, columna, "Error Sintactico", "Caracter no esperado");
        listaErrorSintacticos.add(datos);
    }

    //Metodo al que se llama en el momento en que ya no es posible una recuperacion de errores
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception
    {        
        String lexema = s.value.toString();
        int fila = s.right;
        int columna = s.left;
        
        System.out.println("!!!!!!! Error Sintactico, Panic Mode !!!!!!! ");
        System.out.println("\t\tLexema: "+lexema);
        System.out.println("\t\tFila: "+fila);
        System.out.println("\t\tColumna: "+columna);

        MyError datos = new MyError(lexema, fila, columna, "Error Sintactico", "Simbolo no esperado, Panic Mode");
        listaErrorSintacticos.add(datos);
    }

:}

//---------> Codigo para las acciones gramaticales
action code
{:
:}

/*--------- 2da Area: Declaraciones---------*/

//---------> Declaraciones de terminales
terminal TYPE, INT, INTPTR, CHAR, CHARPTR, BOOL, BOOLPTR, STRING, ARROW, FUNCTION, MAIN, RETURN, READ, PRINT, IF, ELSE, FOR, WHILE, DO, SWITCH, CASE, DEFAULT, BREAK, ID, ADDFOR, MINUSFOR, MULTFOR;
terminal MULT_OP, DIV_OP, ADD_OP, MINUS_OP, ASSIGN, ADD_ASSIGN, MINUS_ASSIGN, MULT_ASSIGN, DIV_ASSIGN, EQUAL_OP, COMMA, AND_OP, OR_OP, LESSTHAN_OP, GREATERTHAN_OP, LESSEQUAL_OP;
terminal GREATEREQUAL_OP, NOTEQUAL_OP, LPAR, RPAR, LBRAC, RBRAC, LKEY, RKEY, SEMICOLON, COLON, DOT, DASH, LETTER, INTEGER, TRUE, FALSE, SIMCOM, DOBCOM, SYMBS, TEXT;

//---------> Declaraciones de no terminales
non terminal String INICIO, declaracion, asignacionInteger, instanciacionInteger, iniciacionInteger, instanciacionIntPtr, integerLista, instanciacionCharPtr;
non terminal asignacionChar, instanciacionChar, iniciacionChar, charComillas, charLista, asignacionBool, instanciacionBool, iniciacionBool, bool, instanciacionBoolPtr, boolLista;
non terminal asignacionString, instanciacionString, iniciacionString, contenidoString, asignacionIntegerPtr, instanciacionIntegerPtr, iniciacionIntegerPtr;
non terminal asignacionCharPtr, instanciacionCharPointer, iniciacionCharPtr, asignacionBoolPtr, instanciacionBoolPointer, iniciacionBoolPtr, core;

non terminal body, body_dec;

//if DEC
non terminal conditionalExpr, andORor, logicalExpr, logicalExprComp, logicalExprID, comparator, if, else, ifElse, ifSinElse;

//Iteraciones DEC
non terminal while, doWhile, for, autoAssign, declaracionFor, instanciacionIntegerFor, switch, caseExist, case, declaracionCase, varCase, caseBreak, default;
non terminal dec_function, parameters, params_type, varRet, dec_main, arithmetic_exp, term, factor, var_type, read, func, assign_var, entry, salida;
non terminal content_print, more_content, var_entry, id_text, declaracion_ptrs_iniciacion, dec_id, ids, moreIds;

non terminal linea;

//---------> Precedencia de menor a mayor
precedence left ADD_OP, MINUS_OP;
precedence left MULT_OP, DIV_OP;

start with INICIO;

/*--------- 3ra Area: Reglas Semanticas---------*/
INICIO ::= INICIO linea
    | linea;

linea ::= dec_function
    | dec_main {::}
    | declaracion;

declaracion ::= asignacionInteger:a DASH {: RESULT = (String)a; :}
    | asignacionInteger error {: System.out.println("Expected - (dash)."); :}
    | asignacionIntegerPtr DASH {::} 
    | asignacionIntegerPtr error {: System.out.println("Expected - (dash)."); :}
    | asignacionChar DASH
    | asignacionChar error {: System.out.println("Expected - (dash)."); :}
    | asignacionCharPtr DASH
    | asignacionCharPtr error {: System.out.println("Expected - (dash)."); :}
    | asignacionBool DASH
    | asignacionBool error {: System.out.println("Expected - (dash)."); :}
    | asignacionBoolPtr DASH
    | asignacionBoolPtr error {: System.out.println("Expected - (dash)."); :}
    | asignacionString DASH
    | asignacionString error {: System.out.println("Expected - (dash)."); :};

/*------------------- INT GRAMATICA -----------------------*/
asignacionInteger ::= asignacionInteger COMMA instanciacionInteger {::} 
    | INT instanciacionInteger;

instanciacionInteger ::= ID iniciacionInteger 
    | error iniciacionInteger {: System.out.println("Expected an id."); :};

iniciacionInteger ::= ASSIGN INTEGER
    | ASSIGN error {: System.out.println("Expected a number."); :}
    | ;

integerLista ::= INTEGER {::}
    | integerLista COMMA INTEGER
    | integerLista COMMA error {: System.out.println("Expected a number."); :}
    | error {: System.out.println("Expected a number."); :};

asignacionIntegerPtr ::= asignacionIntegerPtr COMMA instanciacionIntegerPtr {::} 
    | INTPTR instanciacionIntegerPtr;

instanciacionIntegerPtr ::= ID iniciacionIntegerPtr 
    | error iniciacionIntegerPtr {: System.out.println("Expected an id."); :};

iniciacionIntegerPtr ::= ASSIGN instanciacionIntPtr
    | ;

instanciacionIntPtr ::= LBRAC RBRAC {::}
    | LBRAC integerLista RBRAC;

/*------------------- CHAR GRAMATICA -----------------------*/
asignacionChar ::= asignacionChar COMMA instanciacionChar {::} 
    | CHAR instanciacionChar;

instanciacionChar ::= ID iniciacionChar 
    | error iniciacionChar {: System.out.println("Expected an id."); :};

iniciacionChar ::= ASSIGN charComillas
    | ;

charComillas ::= SIMCOM LETTER SIMCOM
    | error LETTER SIMCOM {: System.out.println("Expected a '.'"); :}
    | SIMCOM LETTER error {: System.out.println("Expected a '.'"); :}
    | SIMCOM error SIMCOM {: System.out.println("Expected a char."); :}
    | DOBCOM LETTER DOBCOM 
    | error LETTER DOBCOM {: System.out.println("Expected a '.'"); :}
    | DOBCOM LETTER error {: System.out.println("Expected a '.'"); :}
    | DOBCOM error DOBCOM {: System.out.println("Expected a char."); :};

charLista ::= LETTER {::}
    | charLista COMMA LETTER
    | charLista COMMA error {: System.out.println("Expected a char."); :}
    | error {: System.out.println("Expected a char."); :};

asignacionCharPtr ::= asignacionCharPtr COMMA instanciacionCharPointer {::} 
    | CHARPTR instanciacionCharPointer;

instanciacionCharPointer ::= ID iniciacionCharPtr 
    | error iniciacionCharPtr {: System.out.println("Expected an id."); :};

iniciacionCharPtr ::= ASSIGN instanciacionCharPtr
    | ;

instanciacionCharPtr ::= LBRAC RBRAC {::}
    | LBRAC charComillas RBRAC;

/*------------------- BOOL GRAMATICA -----------------------*/
asignacionBool ::= asignacionBool COMMA instanciacionBool {::}
    | BOOL instanciacionBool;

instanciacionBool ::= ID iniciacionBool 
    | error iniciacionBool {: System.out.println("Expected an id."); :};

iniciacionBool ::= ASSIGN bool
    | ;

bool ::= TRUE | FALSE
    | error {: System.out.println("Expected either true/false."); :};

asignacionBoolPtr ::= asignacionBoolPtr COMMA instanciacionBoolPtr {::}
    | BOOLPTR instanciacionBoolPtr;

instanciacionBoolPtr ::= ID iniciacionBoolPtr 
    | error iniciacionBoolPtr {: System.out.println("Expected an id."); :};

iniciacionBoolPtr ::= ASSIGN instanciacionBoolPtr
    | ;

instanciacionBoolPtr ::= LBRAC RBRAC {::}
    | LBRAC boolLista RBRAC;

boolLista ::= bool {::}
    | boolLista COMMA bool;

/*------------------- STRING GRAMATICA -----------------------*/
asignacionString ::= asignacionString COMMA instanciacionString {::} 
    | STRING instanciacionString;

instanciacionString ::= ID iniciacionString 
    | error iniciacionString {: System.out.println("Expected an id."); :};

iniciacionString ::= ASSIGN DOBCOM contenidoString DOBCOM
    | ;

contenidoString ::= contenidoString ID
    | ID
    | contenidoString COMMA error {: System.out.println("Expected an id."); :}
    | error {: System.out.println("Expected an id."); :};

/*------------------- if GRAMATICA -----------------------*/
conditionalExpr ::= conditionalExpr andORor logicalExpr {::}
    | logicalExpr
    | conditionalExpr andORor LPAR logicalExpr RPAR;

andORor ::= AND_OP {::}
    | OR_OP
    | error {: System.out.println("Unknown character."); :};

logicalExpr ::= ID logicalExprComp {::}
    | INTEGER comparator logicalExprID
    | TRUE
    | FALSE
    | error {: System.out.println("Unexpected character."); :};

logicalExprComp ::= comparator logicalExprID {::}
    | ;

logicalExprID ::= ID {::}
    | INTEGER
    | error {: System.out.println("Unexpected character."); :};

comparator ::= EQUAL_OP {::}
    | LESSTHAN_OP
    | GREATERTHAN_OP
    | LESSEQUAL_OP
    | GREATEREQUAL_OP
    | NOTEQUAL_OP;

if ::= IF LPAR conditionalExpr RPAR LKEY body RKEY else {: RESULT = "if"; :};

else ::= else ELSE ifSinElse {::}
    | ;

ifSinElse ::= IF LPAR conditionalExpr RPAR LKEY body RKEY {::};

/*------------------- While GRAMATICA -----------------------*/
while ::= WHILE LPAR conditionalExpr RPAR LKEY body RKEY {: RESULT = "while"; :};

/*------------------- Do While GRAMATICA -----------------------*/
doWhile ::= DO LKEY body RKEY WHILE LPAR conditionalExpr RPAR DASH {: RESULT = "do while"; :};

/*------------------- For GRAMATICA -----------------------*/
for ::= FOR LPAR declaracionFor SEMICOLON conditionalExpr SEMICOLON ID autoAssign RPAR LKEY body RKEY {: RESULT = "for"; :};

autoAssign ::= ADDFOR
    | MINUSFOR
    | MULTFOR;

declaracionFor ::= declaracionFor COMMA instanciacionIntegerFor {::}
    | INT instanciacionIntegerFor
    | ;

instanciacionIntegerFor ::= ID ASSIGN INTEGER  {::};

/*------------------- Switch GRAMATICA -----------------------*/

switch ::= SWITCH LPAR ID RPAR LKEY caseExist default RKEY {: RESULT = "switch"; :};

caseExist ::= case {::}
    | ;

case ::= case declaracionCase {::}
    | declaracionCase;

declaracionCase ::= CASE varCase COLON body caseBreak {::};

varCase ::= INTEGER {::}
    | SIMCOM LETTER SIMCOM;

caseBreak ::= BREAK DASH {::}
    | ;

default ::= DEFAULT COLON body caseBreak {::}
    | ;

/*------------------- BODY -----------------------*/
body ::= body declaracion {::}
    | body:b if:a {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | body:b while:a {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | body:b doWhile:a {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | body:b for:a {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | body:b switch:a {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | body:b assign_var:a {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | body:b entry:a {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | body:b salida:a {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | ;

/*------------------- Declaracion de funciones -----------------------*/
dec_function ::= FUNCTION ID:nombre ARROW LPAR parameters RPAR LKEY body:b RETURN varRet DASH RKEY DASH {: Nodo raiz = new Nodo("funcion", nombre.toString());
                            System.out.println("RESULT");
                            System.out.println(RESULT);
                            System.out.println("body");
                            System.out.println(b);
                            System.out.println(raiz); :};

func ::= ID ARROW LPAR ids RPAR DASH
    | ID error LPAR ids RPAR DASH           {: System.out.println("Expected '=>'"); :}
    | ID ARROW error ids RPAR DASH          {: System.out.println("Expected '('"); :}
    | ID ARROW LPAR ids error DASH          {: System.out.println("Expected ')'"); :}
    | ID ARROW LPAR ids RPAR error          {: System.out.println("Expected '-' at end of line"); :}
;


dec_main ::= MAIN ARROW LPAR parameters RPAR LKEY body RETURN varRet DASH RKEY
    | MAIN error LPAR parameters RPAR LKEY body RETURN varRet DASH RKEY       {: System.out.println("Expected '=>'"); :}
    | MAIN ARROW error parameters RPAR LKEY body RETURN varRet DASH RKEY      {: System.out.println("Expected '('"); :}
    | MAIN ARROW LPAR parameters error LKEY body RETURN varRet DASH RKEY      {: System.out.println("Expected ')'"); :}
    | MAIN ARROW LPAR parameters RPAR error body RETURN varRet DASH RKEY      {: System.out.println("Expected '{'"); :}
    | MAIN ARROW LPAR parameters RPAR LKEY body error varRet DASH RKEY        {: System.out.println("Missing return statement"); :}
    | MAIN ARROW LPAR parameters RPAR LKEY body RETURN varRet error RKEY      {: System.out.println("Expected '-' at end of line"); :}
    | MAIN ARROW LPAR parameters RPAR LKEY body RETURN varRet DASH error      {: System.out.println("Expected ')'"); :}
;
 
parameters ::= parameters COMMA params_type ID
    | params_type ID;

ids ::= moreIds ID;

moreIds ::= moreIds ID COMMA 
    | ;


params_type ::= INT 
    | INTPTR
    | CHAR
    | CHARPTR
    | BOOL
    | BOOLPTR
    | STRING
    | error         {: System.out.println("Expected type"); :};

varRet ::= ID
    | TRUE
    | FALSE
    | INTEGER
    | error         {: System.out.println("Expected return variable"); :};


/* --------------------- Operaciones Aritmeticas ---------------------------*/
assign_var ::= ID ASSIGN arithmetic_exp DASH {: RESULT = "assign"; :};

arithmetic_exp ::= term:term                                                           
            |arithmetic_exp:se1 ADD_OP:pl term:term      
            |arithmetic_exp:se1 MINUS_OP:min term:term                                        
            |ADD_OP term:term                                                   
            |MINUS_OP:min term:term;    

term ::= factor:fact                                                           
         |term:term MULT_OP:tim factor:fact  
         |term:term DIV_OP:div factor:fact;

factor ::= INTEGER
    | ID
    | TRUE
    | FALSE
    | LPAR arithmetic_exp RPAR;                                                       
       
    
/* -------------------- Lectura y Escritura ---------------------------*/
entry ::= READ LPAR ID RPAR DASH        {: RESULT = "entry"; :}
    | READ error ID RPAR DASH           {: System.out.println("Expected '('"); :}
    | READ LPAR error RPAR DASH         {: System.out.println("Expected in-variable"); :}
    | READ LPAR ID error DASH           {: System.out.println("Expected ')'"); :}
    | READ LPAR ID RPAR error            {: System.out.println("Expected '-' at end of line"); :}
;

salida ::= PRINT LPAR content_print:c RPAR DASH    {: System.out.println("SALIDA");
                                                    System.out.println(c);
                                                    RESULT = "salida"; :}
    | PRINT error content_print RPAR DASH           {: System.out.println("Expected '('"); :}
    | PRINT LPAR content_print error DASH           {: System.out.println("Expected ')'"); :}
    | PRINT LPAR content_print RPAR error            {: System.out.println("Expected '-' at end of line"); :}
;


content_print ::= id_text:a more_content:b {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :};

more_content ::= ADD_OP id_text:a more_content:b {: if(b != null) RESULT = (String)a + ' ' + (String)b; else RESULT = (String)a; :}
    | ;

id_text ::= ID:id {: RESULT = id; :}
    | TEXT:id {: RESULT = id; :}
    | error            {: System.out.println("Expected printing content"); :};
